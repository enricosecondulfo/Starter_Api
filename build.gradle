group 'com.splitpix.templates.web.api'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = '1.4.0.M3'
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'spring-boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    baseName = rootProject.name.toLowerCase()
    version = '0.0.1-SNAPSHOT'
}

war {
    baseName = rootProject.name.toLowerCase()
    version = '0.0.1-SNAPSHOT'
    archiveName =  "${baseName}.${extension}"
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spring.io/libs-milestone'
    }
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

ext {
    names = "${group}.${rootProject.name.toLowerCase()}"
}

dependencies {

    /* Init */
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-undertow"

    /* Persistence */
    compile "org.springframework.boot:spring-boot-starter-data-mongodb"

    /* Utils */
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    compile "com.google.guava:guava:19.0"

    /* Test */
    testCompile "org.springframework.boot:spring-boot-starter-test"
}

task deploy(type: Copy, dependsOn: 'war') {
    from 'build/libs'

    def wildflyHome = System.getenv("WILDFLY_HOME");

    if (wildflyHome == null) {
        throw new GradleException("WILDFLY_HOME is not set");
    }

    def folderUrlDeployment = "$wildflyHome/standalone/deployments";
    into folderUrlDeployment
}